plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
}

base {
    archivesName = "geckolib-examples-ml-common-${minecraft_version}"
}

minecraft {
    version(minecraft_version)

    // Because Minecraft makes BlockEntities impossible to register in common without it
    accessWideners(file("src/main/resources/${mod_id}.accesswidener"))
}

dependencies {
    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
    implementation "software.bernie.geckolib:geckolib-common-${minecraft_version}:${geckolib_version}"
}

// Don't copy this for your mod, this is for GeckoLib internal handling only
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId "software.bernie.geckolib"
            artifactId base.archivesName.get()

            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.artifactId.text() == "geckolib-common-${minecraft_version}"
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }

    repositories {
        if (System.getenv("cloudUsername") == null && System.getenv("cloudPassword") == null) {
            mavenLocal()
        }
        else maven {
            name = "cloudsmith"
            url = "https://maven.cloudsmith.io/geckolib3/geckolib/"
            credentials {
                username = System.getenv("cloudUsername")
                password = System.getenv("cloudPassword")
            }
        }
    }
}